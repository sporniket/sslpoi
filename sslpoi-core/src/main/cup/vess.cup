// CUP specification for VErbose SpornyScript (vess)
// [FIXME] for now use a sample spec to get the build up and running.
// CUP specification for a simple expression evaluator (w/ actions)

import java_cup.runtime.*;
import com.sporniket.scripting.sslpoi.core.*;
import com.sporniket.scripting.sslpoi.mass.*;

/* Terminals (tokens returned by the scanner). */
terminal A;
terminal AS;
terminal BRACKET_SQR_L; // '['
terminal BRACKET_SQR_R; // ']'
terminal CALL;
terminal COMMA; // ','
terminal DEFINE;
terminal ELSE;
terminal ELSEIF;
terminal ENDIF;
terminal ENDON;
terminal FROM;
terminal IDENTIFIER;
terminal IF;
terminal IS;
terminal LIKE;
terminal LITERAL_STRING;
terminal NAMED;
terminal NEW;
terminal NOT;
terminal ON;
terminal PACKAGEPREFIX;
terminal USING;
terminal WITH;

/* Non-terminals */
non terminal expr_access_stack;
non terminal expr_classname;
non terminal expr_from;
non terminal expr_literal_string;
non terminal expr_logic;
non terminal expr_logic__operator;
non terminal expr_logic__operator_base;
non terminal expr_methodname;
non terminal expr_value;
non terminal part_if_alternatives;
non terminal part_mapping__list;
non terminal part_mapping__single;
non terminal part_using__list;
non terminal part_using__single;
non terminal statm__list;
non terminal statm__single;
non terminal statm_call;
non terminal statm_define;
non terminal statm_if;
non terminal statm_on;

/* Precedences */
//precedence left PLUS, MINUS;
//precedence left TIMES, DIVIDE, MOD;
//precedence left UMINUS;

//======== GRAMMAR ========
//==== Statements break down ====
statm__list 
	::= 
		statm__single:statm statm__list:other 
		{: 
			((VessNode) statm).setNext((VessNode) other) ; RESULT = statm ; 
		:} 
	|
		statm__single:statm 
		{: 
			RESULT = statm ; 
		:}
	;

statm__single
	::=
		statm_call:statm 
		{: 
			RESULT = statm ; 
		:}
	|
		statm_define:statm 
		{: 
			RESULT = statm ; 
		:}
	|
		statm_if:statm 
		{: 
			RESULT = statm ; 
		:}
	|
		statm_on:statm 
		{: 
			RESULT = statm ; 
		:}
	;

//==== Statement types ====
statm_call
	::=
		CALL expr_methodname:meth USING part_using__list:using
		{:
			RESULT = new VessNodeCall().withCall((VessNodeAccessor)meth).withMapping((VessNodeArgumentMapping) using) ;
		:}
	|
		CALL expr_methodname:meth
		{:
			RESULT = new VessNodeCall().withCall((VessNodeAccessor)meth) ;
		:}
	;

statm_define 
	::= 
		DEFINE IDENTIFIER:id AS NEW expr_classname:cls 
		{: 
			RESULT = new VessNodeDefineAs((String)id, InitialisationMode.NEW, (String)cls) ; 
		:}
	|
		DEFINE IDENTIFIER:id AS NEW expr_classname:cls BRACKET_SQR_L BRACKET_SQR_R
		{: 
			RESULT = new VessNodeDefineAs((String)id, InitialisationMode.NEW, (String)cls).withArray(true) ; 
		:}
	;

statm_if
	::=
		IF expr_logic:test statm__list:list ENDIF
		{:
			RESULT = new VessNodeIf().withTest((VessNodeExpressionLogical) test).withStatement((VessNode) list);
		:}
	|
		IF expr_logic:test statm__list:list part_if_alternatives:alternatives
		{:
			RESULT = new VessNodeIf().withTest((VessNodeExpressionLogical) test).withStatement((VessNode) list).withAlternative((VessNodeIf) alternatives);
		:}
	;


statm_on
	::=
		ON IDENTIFIER:id statm__list:list ENDON
		{:
			RESULT = new VessNodeOn().withEventName((String) id).withStatements((VessNode) list);
		:}
	|
		ON IDENTIFIER:id WITH part_mapping__list:mapping statm__list:list ENDON
		{:
			RESULT = new VessNodeOn().withEventName((String) id).withStatements((VessNode) list).withMapping((VessNodeIdentifierMapping) mapping) ;
		:}
	;

//== Statement parts ==
part_if_alternatives
	::=
		ELSEIF expr_logic:test statm__list:list part_if_alternatives:alternatives
		{:
			RESULT = new VessNodeIf().withTest((VessNodeExpressionLogical) test).withStatement((VessNode) list).withAlternative((VessNodeIf) alternatives);
		:}
	|
		ELSEIF expr_logic:test statm__list:list ENDIF
		{:
			RESULT = new VessNodeIf().withTest((VessNodeExpressionLogical) test).withStatement((VessNode) list);
		:}
	|
		ELSE statm__list:list ENDIF
		{:
			RESULT = new VessNodeIf().withStatement((VessNode) list);
		:}
	;

part_mapping__list
	::=
		part_mapping__single:single COMMA part_mapping__single:list
		{:
			RESULT = ((VessNode)single).withLastNode((VessNode)list);
		:}
	|
		part_mapping__single:single
		{:
			RESULT = single ;
		:}
	;

part_mapping__single
	::=
		A expr_classname:cls NAMED IDENTIFIER:id
		{:
			RESULT = new VessNodeIdentifierMapping().withIdentifier((String)id).withClassName((String)cls) ;
		:}
	|
		A expr_classname:cls BRACKET_SQR_L BRACKET_SQR_R NAMED IDENTIFIER:id
		{:
			RESULT = new VessNodeIdentifierMapping().withIdentifier((String)id).withClassName((String)cls).withArray(true) ;
		:}
	;

part_using__list
	::=
		part_using__single:single COMMA part_using__list:list
		{:
			RESULT = ((VessNode)single).withLastNode((VessNode)list);
		:}
	|
		part_using__single:single
		{:
			RESULT = single ;
		:}
	;

part_using__single
	::=
		expr_value:val
		{:
			RESULT = new VessNodeArgumentMapping().withValue((VessNode)val);
		:}
	;

//==== Expressions ====
expr_classname 
	::= 
		PACKAGEPREFIX:pack IDENTIFIER:cls 
		{: 
			RESULT = ((String)pack)+((String)cls) ; 
		:}
	|
		IDENTIFIER:cls 
		{: 
			RESULT = cls ; 
		:} 
	;

expr_from
	::=
		FROM IDENTIFIER:id expr_from:from 
		{: 
			VessNodeAccessor _node = new VessNodeAccessor().withValue((String)id);
			RESULT = ((VessNode)from).withLastNode(_node);
		:}
	|
		FROM IDENTIFIER:id 
		{:
			RESULT = new VessNodeAccessor().withValue((String)id);
		:}
	;
	
expr_access_stack
	::=
		IDENTIFIER:id expr_from:from 
		{: 
			VessNodeAccessor _node = new VessNodeAccessor().withValue((String)id);
			RESULT = ((VessNode)from).withLastNode(_node);
		:}
	|
		IDENTIFIER:id
		{: 
			RESULT = new VessNodeAccessor().withValue((String)id);
		:}
	;
	
expr_literal_string
	::=
		LITERAL_STRING:value
		{:
			RESULT = new VessNodeLiteralString().withValue((String) value);
		:}
	;

expr_logic
	::=
		expr_value:value expr_logic__operator:op expr_value:expected
		{:
			RESULT = new VessNodeExpressionLogical().withValue((VessNodeValue) value).withOperator((VessNodeOperatorLogical) op).withExpected((VessNodeValue) expected);
		:}
	;

expr_logic__operator
	::=
		IS
		{:
			RESULT = new VessNodeOperatorLogical().withOperator(LogicalOperator.IS) ;
		:}
	|
		IS NOT
		{:
			RESULT = new VessNodeOperatorLogical().withOperator(LogicalOperator.IS).withNot(true) ;
		:}
	|
		IS expr_logic__operator_base:op
		{:
			RESULT = new VessNodeOperatorLogical().withOperator((LogicalOperator) op) ;
		:}
	|
		IS NOT expr_logic__operator_base:op
		{:
			RESULT = new VessNodeOperatorLogical().withOperator((LogicalOperator) op).withNot(true) ;
		:}
	;

expr_logic__operator_base
	::=
		LIKE
		{:
			RESULT = LogicalOperator.IS_LIKE ;
		:}
	;
expr_methodname
	::=
		expr_access_stack:stack
		{:
			RESULT = stack ;
		:}
	;
		
	
expr_value
	::=
		expr_access_stack:value
		{:
			RESULT = value ;
		:}
	|
		expr_literal_string:value
		{:
			RESULT = value ;
		:}
	;
